Use Windows.pkg
Use cCJStandardMenuItemClasses.pkg
Use cCJDeoMenuItemClasses.pkg
Use Tools\TableProperties.dg
Register_Object oTH3WorkspaceConfigure

Object oDatabaseContextMenu is a cCJContextMenu
    Property String psFileName ""
    Property Handle phTableNumber 0
    Property Handle phInvokingObject 0

    Function ProcessRunMacros String sEditor Returns String
        Handle hTable hoClient
        String sVdfRoot sTable sParam
        Get phTableNumber to hTable
        Get psVdfRootDir of ghoWorkSpaceHandlerEx to sVdfRoot
        Move (Uppercase(sEditor)) to sEditor
        If (Uppercase(sEditor) contains "@VDFROOT@") ;
            Move (Replace("@VDFROOT@", sEditor, sVDFRoot)) to sEditor
        If (Uppercase(sEditor) contains "@TABLENO@") ;
            Move (Replace("@TABLENO@", sEditor, String(hTable))) to sEditor
        If (Uppercase(sEditor) contains "@TABLENAME@") Begin
            Get_Attribute DF_FILE_LOGICAL_NAME of hTable to sTable
            Move (Replace("@TABLENAME@", sEditor, sTable)) to sEditor
        End
        If (Uppercase(sEditor) contains "@WORKSPACE@") Begin
            Get Client_Id to hoClient
            Get InsertCurrentWorkspace of hoClient "" to sParam
            Move (Replace(" -X", sEditor, " ")) to sEditor
            Move (Replace("@WORKSPACE@", sEditor, sParam)) to sEditor
        End
        Function_Return sEditor
    End_Function

    Object oEditItem is a cCJMenuItem
        Set psCaption   to "Edit Table"
        Set psToolTip to "Edit Table"
        Set psDescription to "Edit Table"
        Set psImage to "TH3EditTable16.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            String sEditor
            Move gtTHWorkspace.sDBEditor to sEditor
            If (sEditor="") Begin
                Get psDBBPath of ghoEditorProperties to sEditor
                If (sEditor="") Begin
                    Send Stop_Box "TheHammer Database Edit Tool not specified" // Take from defaults
                    Send Popup to oTH3WorkspaceConfigure
                    Move gtTHWorkspace.sDBEditor to sEditor
                    If (sEditor="") Procedure_Return
                End
            End
            Get ProcessRunMacros sEditor to sEditor
            //
            Runprogram background sEditor ""
        End_Procedure
    End_Object
    
    Object oViewItem is a cCJMenuItem
        Set psCaption   to "View Table"
        Set psToolTip to "View Table"
        Set psDescription to "View Table"
        Set psImage to "TH3ViewTable16.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            String sViewer
            Move gtTHWorkspace.sDBViewer to sViewer
            If (sViewer="") Begin
                Get psDBEPath of ghoEditorProperties to sViewer
                If (sViewer="") Begin
                    Send Stop_Box "TheHammer Database Viewer Tool not specified" // Take from defaults
                    Send Popup to oTH3WorkspaceConfigure
                    Move gtTHWorkspace.sDBViewer to sViewer
                    If (sViewer="") Procedure_Return
                End
            End
            Get ProcessRunMacros sViewer to sViewer
            //
            Runprogram background sViewer ""
        End_Procedure
    End_Object
    
    Object oNewItem is a cCJMenuItem
        Set psCaption   to "Create New Table"
        Set psToolTip to "Create New Table"
        Set psDescription to "Create New Table"
        Set psImage to "TH3NewTable16.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            String sEditor sParam
            Handle hoClient
            Move gtTHWorkspace.sDBEditor to sEditor
            If (sEditor="") Begin
                Get psDBBPath of ghoEditorProperties to sEditor
                If (sEditor="") Begin
                    Send Stop_Box "TheHammer Database Edit Tool not specified" // Take from defaults
                    Send Popup to oTH3WorkspaceConfigure
                    Move gtTHWorkspace.sDBEditor to sEditor
                    If (sEditor="") Procedure_Return
                End
            End
            Move (Uppercase(sEditor)) to sEditor
            If ((sEditor contains " -T@TABLENO@")or(sEditor contains " -T@TABLENAME@")) Begin
                Move (Replace("-T@TABLENO@",   sEditor, "")) to sEditor
                Move (Replace("-T@TABLENAME@", sEditor, "")) to sEditor
            End
            Get ProcessRunMacros sEditor to sEditor
            Move (Trim(sEditor) * "-PNEWTABLE") to sEditor
            //
            Get Client_Id to hoClient
            Get InsertCurrentWorkspace of hoClient "" to sParam
            //
            Runprogram background sEditor sParam
        End_Procedure
    End_Object
    
    Object oFolderItem is a cCJMenuItem
        Set pbControlBeginGroup to True
        Set psCaption   to "Open Containing Folder"
        Set psToolTip to "Open Containing Folder"
        Set psDescription to "Open Containing Folder"
        Set psImage to "ActionOpen.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            Boolean bOpened
            Handle hTable
            String sTable sDataPath sOldFileList sFileList
            Get phTableNumber to hTable

            Move (CurrentFileListPath(ghoWorkSpaceHandlerEx)) to sFileList
            Get_FileList sOldFileList
            Set_FileList sFileList
            Get_Attribute DF_FILE_ROOT_NAME of hTable to sTable
            Set_FileList sOldFileList
            Get psBufferDataPath of ghoWorkSpaceHandlerEx   to sDataPath
            //
            If (Right(sDataPath, 1)<>"\") Move (sDataPath+"\") to sDataPath
            // Identify DataFlex file
            If ((not(sTable contains ":")) and (not(sTable contains "."))) Move (sTable+".DAT") to sTable
            // Driver name workout (XXXXDRV:TABLE)
            If (sTable contains ":") Begin
                Move (Right(sTable, Length(sTable)-Pos(":", sTable))) to sTable
                Move (sTable+".INT") to sTable
            End
            // open explorer in default style at correct path, with filename selected.
            Runprogram background "Explorer" ("/e,/select,"+sDataPath+sTable)
        End_Procedure
    End_Object
    
    Object oDDItem is a cCJMenuItem
        Set psCaption   to "Open Data Dictionary"
        Set psToolTip to "Open Data Dictionary"
        Set psDescription to "Open Data Dictionary"
        Set psImage to "TH3OpenDD16.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            tDBDDClasses    ltDDClass
            tDBDDClasses[]  ltDDClasses
            Handle          hoDBTV
            Handle          hTable
            Integer         iIndex
            String          sPathFile
            Get phTableNumber       to hTable
            Get phInvokingObject    to hoDBTV
            If (hoDBTV) Begin
                Get ptDDClasses of hoDBTV to ltDDClasses
                Move hTable to ltDDClass.iTableNo
                Move (SearchArray(ltDDClass, ltDDClasses)) to iIndex
                If (iIndex>=0) Begin
                    Get psBufferDdSrcPath of ghoWorkSpaceHandlerEx to sPathFile
                    Send CAOpenFile of (oClientArea(oMain(Self))) (sPathFile+'\'+ltDDClasses[iIndex].sDDFile)
                End
            End
        End_Procedure
    End_Object
    
    Object oPropertiesItem is a cCJMenuItem
        Set pbControlBeginGroup to True
        Set psCaption   to "Table Properties..."
        Set psToolTip to "Table Properties"
        Set psDescription to "Table Properties"
        Set psImage to "Properties16.ico"
        Set psCategory to C_$CategoryFile
        Procedure OnExecute Variant vCommandBarControl
            Set phTable of oTableProperties to (phTableNumber(Self))
            Send Popup to oTableProperties
        End_Procedure
    End_Object
    
    Procedure OnPopupInit Variant vCommandBarControl Handle hoCommandBarControls
        Forward Send OnPopupInit
        Set ComVisible of oNewItem to (gtTHWorkspace.sSchema="DataFlex")
    End_Procedure
    
End_Object