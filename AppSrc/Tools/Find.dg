Use Windows.pkg
Use cParser.pkg

Define FindUp   for 0
Define FindDown for 1

Struct tFindOptions
  String  sSearchText
  Boolean bWordMatch
  Boolean bMatchCase
  Boolean bRegExp
  Integer eDirection
  Boolean bMarkAll
  Boolean bStartFind
  Boolean bCancel
End_Struct


Class cFindCheckBox is a CheckBox
  
  Procedure doToggle
    Boolean bState
    
    Send DispatchHotkey  // no ringing bell on pressing your shortcut key please, that drives me nuts
    Send Activate
    Get Checked_State to bState
    Set Checked_State to (not(bState))
  End_Procedure
End_Class

Class cFindForm is a Form
  Procedure Activate returns integer
    Integer iRetVal
    Send DispatchHotkey  // no ringing bell on pressing your shortcut key please, that drives me nuts
    forward Get msg_activate to iRetVal
    Procedure_Return iRetVal
  End_Procedure
End_Class




Object oFindDialog is a ModalPanel
    Set Size to 75 270
    Set Label to "Find"
    Set piMinSize to 75 270
    Set Location to 44 47
    Set Border_Style To Border_Thick
    //Set Locate_Mode to POPUP_LOCATE -- locate mode isn't smart enough
    Property Boolean pbStartFind
    Property Boolean pbMarkAll

    Set peAnchors to anAll
    Set piMaxSize to 75 8000

    Object oSearchFm is a cFindForm
      Set Size to 13 160
      Set Location to 10 45
      Set Label to "W&hat:"
      Set Label_Col_Offset to 2
      Set Label_Justification_Mode to JMode_Right
      Set peAnchors to anTopLeftRight
    End_Object

    Object oWordMatchCb is a cFindCheckBox
      Set Size to 10 50
      Set Location to 26 45
      Set Label to "Match &whole word only"
    End_Object

    Object oMatchCaseCb is a cFindCheckBox
      Set Size to 10 50
      Set Location to 42 45
      Set Label to "Match &case"
    End_Object

    Object oRegExCb is a cFindCheckBox
      Set Size to 10 50
      Set Location to 58 45
      Set Label to "Regular &expression"
    End_Object

    Object oDirectionGrp is a RadioGroup
      Set Size to 45 65
      Set Location to 24 140
      Set Label to "Direction"
        Set peAnchors to anTopLeftRight
  
      Object oUpRadio is a Radio
        Set Size to 10 50
        Set Location to 14 10
        Set Label to "&Up"
      End_Object
  
      Object oDownRadio is a Radio
        Set Size to 10 50
        Set Location to 26 10
        Set Label to "&Down"
      End_Object
    End_Object

    Object oFindBtn is a Button
      Set Label to "&Find"
      Set Location to 10 215
      Set peAnchors to anTopRight
      Set Default_State to True

      Procedure OnClick
        Set pbStartFind to True
        Send Close_Panel
      End_Procedure
    End_Object

    Object oMarkAllBtn is a Button
      Set Location to 26 215
      Set peAnchors to anTopRight
      Set Label to "&Mark All"
  
      Procedure OnClick
        Set pbStartFind to True
        Set pbMarkAll   to True
        Send Close_Panel
      End_Procedure
    End_Object

    Object oCancelBtn is a Button
      Set Label    to "Cancel"
      Set Location to 42 215
      Set peAnchors to anTopRight

      Procedure OnClick
          Send Close_Panel
      End_Procedure

    End_Object

    On_Key Key_Alt+Key_H Send Activate  of oSearchFm
    On_Key Key_Alt+Key_W Send doToggle  of oWordMatchCb
    On_Key Key_Alt+Key_C Send doToggle  of oMatchCaseCb
    On_Key Key_Alt+Key_E Send doToggle  of oRegExCb
    //On_Key Key_Alt+Key_U Send Activate  of oUpRadio
    //On_Key Key_Alt+Key_D Send Activate  of oDownRadio
    On_Key Key_Alt+Key_F Send KeyAction of oFindBtn
    On_Key Key_Alt+Key_M Send KeyAction of oMarkAllBtn
    On_Key kCancel       Send KeyAction of oCancelBtn
    
    Procedure RequestDetails tFindOptions Byref Options
      Boolean bStartFind
      
      Set pbStartFind to False
      set pbMarkAll   to False
      Set Value         of oSearchFm     to Options.sSearchText
      Set Checked_State of oWordMatchCb  to Options.bWordMatch
      Set Checked_State of oMatchCaseCb  to Options.bMatchCase
      Set Checked_State of oRegExCb      to Options.bRegExp
      Send Popup
      Get pbStartFind to bStartFind
      If (bStartFind) Begin
        Move True to Options.bStartFind
        Get Value         of oSearchFm     to Options.sSearchText
        Get Checked_State of oWordMatchCb  to Options.bWordMatch
        Get Checked_State of oMatchCaseCb  to Options.bMatchCase
        Get Checked_State of oRegExCb      to Options.bRegExp
        Get Current_Radio of oDirectionGrp to Options.eDirection
        Get pbMarkAll                      to Options.bMarkAll
      End
    End_Procedure

End_Object

