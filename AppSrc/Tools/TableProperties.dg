Use Windows.pkg
Use dfBitmap.pkg

Class cTablePropertyForm is a Form
    Procedure Construct_Object
        Forward Send Construct_Object
        Set Label_Col_Offset to 2
        Set Label_Justification_Mode to JMode_Right
        Set Color to clBtnFace
    End_Procedure
    Procedure Key Integer iKey Returns Integer
        Integer iRetVal
        // Left, Right, Shft+Left, Shift+Right, Home, End, Shft+Home, Shft+End
        If ((iKey=260)or(iKey=261)or(iKey=5127)or(iKey=5128)or ;
            (iKey=294)or(iKey=295)or(iKey=5129)or(iKey=5130) ) ;
        Forward Get Msg_Key iKey to iRetVal
        Procedure_Return iRetVal
    End_Procedure
    Function CanPaste Returns Boolean
        Function_Return False
    End_Function
    Function CanCut Returns Boolean
        Function_Return False
    End_Function
    Function CanDelete Returns Boolean
        Function_Return False
    End_Function
    Function CanUndo Returns Boolean
        Function_Return False
    End_Function
    Procedure Paste Integer Arg1 Integer Arg2 Integer Arg3
    End_Procedure
    Procedure Cut Integer Arg1 Integer Arg2 Integer Arg3
    End_Procedure
    Procedure Delete
    End_Procedure
    Procedure Undo
    End_Procedure
End_Class

Object oTableProperties is a ModalPanel
    Property Handle phTable 0
    Set Size to 260 240
    Set Label to "Table Properties"
    Set piMinSize to 260 240
    Set Location to 2 2
    Set piMaxSize to 260 240

    Object oClose_Btn is a Button
        Set Label    to "&Close"
        Set Location to 242 186
        Set peAnchors to anBottomRight

        Procedure OnClick
            Send Close_Panel
        End_Procedure

    End_Object

    Object oTable is a Group
        Set Size to 74 230
        Set Location to 2 6
        Set Label to "Table"

        Object oTableNo is a cTablePropertyForm
            Set Size to 13 40
            Set Location to 10 75
            Set Label to "Table"
        End_Object

        Object oLogicalName is a cTablePropertyForm
            Set Size to 13 100
            Set Location to 10 120
        End_Object

        Object oDisplayName is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 26 75
            Set Label to "Display name"
        End_Object

        Object oRootName is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 42 75
            Set Label to "Root name"
        End_Object

        Object oSystemTable is a CheckBox
            Set Size to 10 50
            Set Location to 58 75
            Set Label to "System table"
            Set Enabled_State to False
        End_Object

        Object oPanelIcon is a BitmapContainer
            Set Size to 17 18
            Set Location to 10 7
            Set Border_Style to Border_None
            Set Bitmap to "TableInfo32.bmp"
        End_Object
    End_Object

    Object oAttributes is a Group
        Set Size to 94 230
        Set Location to 76 6
        Set Label to "Attributes"

        Object oDriver is a cTablePropertyForm
            Set Size to 13 60
            Set Location to 10 75
            Set Label to "Driver"
        End_Object

        Object oRevision is a cTablePropertyForm
            Set Size to 13 50
            Set Location to 10 169
            Set Label to "Revision"
        End_Object

        Object oOwner is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 42 75
            Set Label to "Owner"
        End_Object

        Object oLogin is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 58 75
            Set Label to "Login"
        End_Object

        Object oType is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 74 75
            Set Label to "Type"
        End_Object

        Object oConnectionID is a cTablePropertyForm
            Set Size to 13 145
            Set Location to 26 75
            Set Label to "Connection ID"
        End_Object
    End_Object

    Object oStatistics is a Group
        Set Size to 68 230
        Set Location to 171 6
        Set Label to "Statistics"

        Object oLength is a cTablePropertyForm
            Set Size to 13 50
            Set Location to 42 75
            Set Label to "Record length"
        End_Object
        Object oMaxLength is a cTablePropertyForm
            Set Size to 13 60
            Set Location to 42 159
            Set Label to "of"
        End_Object
        Object oMaxRecords is a cTablePropertyForm
            Set Size to 13 60
            Set Location to 26 159
            Set Label to "of"
        End_Object
        Object oRecords is a cTablePropertyForm
            Set Size to 13 50
            Set Location to 26 75
            Set Label to "Records"
        End_Object
        Object oColumns is a cTablePropertyForm
            Set Size to 13 50
            Set Location to 10 75
            Set Label to "Columns#"
        End_Object
        Object oIndexes is a cTablePropertyForm
            Set Size to 13 60
            Set Location to 10 159
            Set Label to "Indexes#"
        End_Object
    End_Object

    Procedure ClearData
        Set Value           of oTableNo         to "N/A"
        Set Value           of oLogicalName     to "N/A"
        Set Value           of oDisplayName     to "N/A"
        Set Value           of oRootName        to "N/A"
        Set Checked_State   of oSystemTable     to False
        Set Value           of oDriver          to "N/A"
        Set Value           of oRevision        to "N/A"
        Set Value           of oOwner           to "N/A"
        Set Value           of oLogin           to "N/A"
        Set Value           of oColumns         to "N/A"
        Set Value           of oIndexes         to "N/A"
        Set Value           of oType            to "N/A"
        Set Value           of oRecords         to "N/A"
        Set Value           of oMaxRecords      to "N/A"
        Set Value           of oLength          to "N/A"
        Set Value           of oMaxLength       to "N/A"
        Set Value           of oConnectionID    to "N/A"
    End_Procedure

    Procedure ReadData
        Handle hTable
        Boolean bReopen bUnSupported
        String sDataPath sFileList
        String sOldDataPath sOldFileList
        String sTable sDriver sRevision sOwner sLogin
        Integer iFlag
        //
        Get phTable to hTable
        If (hTable=0) Procedure_Return
        Get_Attribute DF_FILE_LOGICAL_NAME of hTable to sTable
        If (sTable<>"") Get_Attribute DF_FILE_OPENED of hTable to bReopen
        If bReopen Close hTable
        //
        Set Value of oTableNo to hTable
        //
        Get_Attribute DF_REPORT_UNSUPPORTED_ATTRIBUTES      to bUnSupported
        Set_Attribute DF_REPORT_UNSUPPORTED_ATTRIBUTES      to False
        //
        Move (CurrentFileListPath(ghoWorkSpaceHandlerEx))   to  sFileList
        Move (CurrentDataPath(ghoWorkSpaceHandlerEx))       to  sDataPath
        Get_FileList                                            sOldFileList
        Get_Attribute DF_OPEN_PATH                          to  sOldDataPath
        If (sFileList<>"") Begin
            Set_Attribute   DF_OPEN_PATH  to sDataPath
            Set_FileList    sFileList    // Set to the new filelist
        End
        //
        Open hTable
        Get_Attribute DF_FILE_LOGICAL_NAME          of hTable   to sTable
        Set Value of oLogicalName                               to sTable
        Get_Attribute DF_FILE_ROOT_NAME             of hTable   to sTable
        Set Value of oRootName                                  to sTable
        Get_Attribute DF_FILE_DISPLAY_NAME          of hTable   to sTable
        Set Value of oDisplayName                               to sTable
        Get_Attribute DF_FILE_IS_SYSTEM_FILE        of hTable   to iFlag
        Set Checked_State of oSystemTable                       to iFlag
        Get_Attribute DF_FILE_DRIVER                of hTable   to sDriver
        Set Value of oDriver                                    to sDriver
        Get_Attribute DF_FILE_REVISION              of hTable   to sRevision
        Set Value of oRevision                                  to sRevision
        Get_Attribute DF_FILE_OWNER                 of hTable   to sOwner
        If (sOwner<>"") Set Value of oOwner                     to sOwner
        Get_Attribute DF_FILE_LOGIN                 of hTable   to sLogin
        If (sLogin<>"") Set Value of oLogin                     to sLogin
        Get_Attribute DF_FILE_RECNUM_TABLE          of hTable   to iFlag
        If (iFlag) Set Value of oType                           to "RECNUM"
        Else Set Value of oType                                 to "STANDARD (No Recnum support)"
        Get_Attribute DF_FILE_NUMBER_FIELDS         of hTable   to iFlag
        Set Value of oColumns                                   to iFlag
        Get_Attribute DF_FILE_LAST_INDEX_NUMBER     of hTable   to iFlag
        Set Value of oIndexes                                   to iFlag
        Get_Attribute DF_FILE_RECORDS_USED          of hTable   to iFlag
        Set Value of oRecords                                   to iFlag
        Get_Attribute DF_FILE_MAX_RECORDS           of hTable   to iFlag
        Set Value of oMaxRecords                                to iFlag
        Get_Attribute DF_FILE_RECORD_LENGTH_USED    of hTable   to iFlag
        Set Value of oLength                                    to iFlag
        Get_Attribute DF_FILE_RECORD_LENGTH         of hTable   to iFlag
        Set Value of oMaxLength                                 to iFlag
        //
        Close hTable
        //
        Set_FileList                    sOldFileList
        Set_Attribute DF_OPEN_PATH  to  sOldDataPath
        Set_Attribute DF_REPORT_UNSUPPORTED_ATTRIBUTES to bUnSupported
        If bReopen Open hTable // Reopen
    End_Procedure

    Procedure Activate Returns Integer
        Integer iRetVal
        Send ClearData
        Send ReadData
        Forward Get Msg_Activate to iRetVal
        Procedure_Return iRetVal
    End_Procedure

    On_Key Key_Alt+Key_C Send KeyAction of oClose_Btn

End_Object
