// oHorizontalSplitter

// Repostitioning the Errorlist with a splitter.
Object oHorizontalSplitter is a cHorizontalSplitter
    Set Size to 2 153
    Set Location to 72 5
    // this procedure is overwritten because normaly the call to
    // DoMoveSelf is done after the DoNotify. In this case, the
    // size of splitters parent is changed and so we need to notify
    // the objects before the splitter is redrawn. Otherwise the
    // splitter is placed on a wrong place and is no longer accessable.
    // 03.03.2003 BP
    Procedure OnSplitterMouseUp Integer wParam Integer lParam
       Integer iMinTrack iMaxTrack iTrack

       Send Release_Mouse_Capture

       Send DoDrawDrag 65536 // Special value to erase last line

       Move (Hi (lParam)) to iTrack
       If (iTrack > 32000) Begin
          Move (iTrack - 65536) to iTrack
       End

       Get piMinTrack to iMinTrack
       Get piMaxTrack to iMaxTrack

       Move (iMaxTrack min (iMinTrack max iTrack)) to iTrack

       Send DoMoveSelf iTrack
       Send DoNotify iTrack
    End_Procedure // OnSplitterMouseUp
End_Object    // oHorizontalSplitter

Send DoAddObjectToNotify to (oHorizontalSplitter(Self)) Self
Procedure OnSplitterChange Integer iTrack
   Integer iPy
   If not (Visible_State(oErrors(Self))) Procedure_Return
   Set MDI_Bottom_Margin of (oClientArea(Self)) to ;
    (MDI_Bottom_Margin(oClientArea(Self))-iTrack)
   //Send FitErrorContainerToMdiMargin false
   Set piErrorListHeight of ghoEditorProperties to (MDI_Bottom_Margin(oClientArea(Self)))
   Send Adjust_Logicals
End_Procedure // OnSplitterChange

// We don't want the splitter to be canceled so return 0 here.
// 03.03.2003 BP
Procedure OnRequestSplitter
    Procedure_Return 0
End_Procedure
