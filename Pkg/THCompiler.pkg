// The Hammer built-in compiler for DataFlex
//

Use VDFCompCtl.pkg

Class cTHCompiler is a cComVDFCompiler
    Procedure Construct_Object
        Forward Send Construct_Object
        Property Integer piCompilingMode 0
        //Set peAutoCreate to acAutoCreate
    End_Procedure
    Procedure AddPath String ByRef sMakePath String sPath
        If (sMakePath="") Move sPath to sMakePath
        Else Move (sMakePath + ';' + sPath) to sMakePath
    End_Procedure
    
    Function CompileMakePath String sVDFRoot Boolean bPrecompile Returns String
        String sMakePath sPath
        If (not(bPrecompile)) Begin
            Get psBufferAppSrcPath    of ghoWorkSpaceHandlerEx   to sPath
            Send AddPath (&sMakePath) sPath
            Get psBufferDdSrcPath     of ghoWorkSpaceHandlerEx   to sPath
            Send AddPath (&sMakePath) sPath
            Get psBufferDataPath      of ghoWorkSpaceHandlerEx   to sPath
            Send AddPath (&sMakePath) sPath
            Get psBufferBitmapPath    of ghoWorkSpaceHandlerEx   to sPath
            Send AddPath (&sMakePath) sPath
        End
        Send AddPath (&sMakePath) (sVDFRoot + "Bin")
        Send AddPath (&sMakePath) (sVDFRoot + "Lib")
        Send AddPath (&sMakePath) (sVDFRoot + "Usr")
        Send AddPath (&sMakePath) (sVDFRoot + "Pkg")
        Send AddPath (&sMakePath) (sVDFRoot + "Bitmaps")
        Function_Return sMakePath
    End_Function
    Function CompileTarget String sSource Returns String
        String sTarget sFile
        Move (FileFromPath(sSource)) to sFile
        If (Right(Uppercase(sFile), 4)=".SRC") ;
            Move (Left(sFile, Length(sFile)-4)+".exe") to sFile
        Get psBufferProgramPath   of ghoWorkSpaceHandlerEx to sTarget
        If (Right(sTarget,1)<>'\') Move (sTarget + '\') to sTarget
        Function_Return (sTarget + sFile)
    End_Function
    Procedure PrepareToCompile Boolean bPrecompile String sSource
        Handle hoClient
        String sVdfRoot sWrapper sMakePath
        Get Client_Id to hoClient
        Get psVdfRootDir of ghoWorkSpaceHandlerEx to sVdfRoot
        If (Right(sVdfRoot,1)<>'\') Move (sVdfRoot + '\') to sVdfRoot
        Move (sVdfRoot + "Bin\" + "DFC.exe") to sWrapper
        If (bPrecompile) Begin
            // If not a System Package, then set App Make Path and System otherwise
            If (not(Uppercase(sSource) contains Uppercase(sVdfRoot))) ;
                Move 0 to bPrecompile
        End
        Get CompileMakePath sVDFRoot bPrecompile to sMakePath
        Set ComCompiler to sWrapper
        Set ComMakePath to sMakePath
        Set piCompilingMode to True
        If (piDisplayWatchAutomatic(ghoEditorProperties)) ;
            Send PAWindowErrorListONOFF to hoClient True
    End_Procedure
    Procedure Compile String sSource
        String sTarget
        If (piCompilingMode(Self)) Procedure_Return
        Send PrepareToCompile (False) sSource
        Get CompileTarget sSource to sTarget
        If ghoCompilerInfo ;
            Send mStart to ghoCompilerInfo ("----" * "Compiling" * sSource * "----")
        Send ComCompile sSource sTarget True
    End_Procedure
    Procedure PreCompile String sPackage
        If (piCompilingMode(Self)) Procedure_Return
        Send PrepareToCompile (True) sPackage
        If ghoCompilerInfo ;
            Send mStart to ghoCompilerInfo ("----" * "Pre-Compiling" * sPackage * "----")
        Send ComPrecompile sPackage True
    End_Procedure
    Procedure OnComEndCompile Boolean llcompileSuccessful
        Handle hoClient
        Get Client_Id to hoClient
        Set piCompilingMode to False
        If ghoCompilerInfo ;
            Send mFinish to ghoCompilerInfo ("---- Compile Finished ----")
        If hoClient Send CACompleteCompileAction to hoClient
    End_Procedure
    Procedure OnComCompileMessage String lltext
        If ghoCompilerInfo ;
            Send AddMessage to ghoCompilerInfo llText 1
    End_Procedure
    Procedure OnComCompileWarning String lltext
        If ghoCompilerInfo ;
            Send AddMessage to ghoCompilerInfo llText 2
    End_Procedure
    Procedure OnComCompileError String lltext
        If ghoCompilerInfo ;
            Send AddMessage to ghoCompilerInfo llText 3
    End_Procedure
    Procedure OnComCriticalError String lltext
        If ghoCompilerInfo ;
            Send AddMessage to ghoCompilerInfo llText 3
    End_Procedure
End_Class

Function BuiltInCompiler Returns Handle
    Handle hoCompiler
    Integer iCompiler
    String sVersion
    // External compiler only will switch off built-in compiler feature
    If (pbExtCompilerOnly(ghoEditorProperties)) Function_Return 0
    //
    Get psVDFVersion of ghoWorkSpaceHandlerEx           to sVersion
    Get THFindBuiltInCompiler sVersion                  to iCompiler
    If (iCompiler>=0) Begin
        If (gtTHCompilers[iCompiler].hoCompiler)    ;
            Move gtTHCompilers[iCompiler].hoCompiler    to hoCompiler
        Else Begin
            Get Create              U_cTHCompiler       to hoCompiler
            Set psProgID            of hoCompiler       to gtTHCompilers[iCompiler].sProgID
            //Set psEventId           of hoCompiler       to gtTHCompilers[iCompiler].sEventId
            Send CreateComObject    of hoCompiler
            // Cache object for re-using
            If (IsComObjectCreated(hoCompiler)) ;
                Move hoCompiler                         to gtTHCompilers[iCompiler].hoCompiler
        End
    End
    Function_Return hoCompiler
End_Function
